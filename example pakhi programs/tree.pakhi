#
Generates tree view of every files and folders from specified path.
This is a lot like tree command in windows systems.
Usage: run this file then type directory you want as root.
#

ফাং ভ্যালিড(পাথ) {
    নাম স্প্লিটেড-পাথ = _স্ট্রিং-স্প্লিট(পাথ, "");
    যদি স্প্লিটেড-পাথ[০] == "." | স্প্লিটেড-পাথ[০] == "$" {
        ফেরত মিথ্যা;
    } অথবা {
        ফেরত সত্য;
    }
} ফেরত;

ফাং ট্রি(রুট, প্যাডিং) {
    নাম ডাইরেক্টরি = _রিড-ডাইরেক্টরি(রুট);
    নাম লেন = _লিস্ট-লেন(ডাইরেক্টরি);
    নাম ক = ০;
    লুপ {
        যদি ক >= লেন {
            থামাও;
        }

        নাম সিম্বল = "├───";
        নাম নেক্সট-প্যাডিং = "│    ";
        যদি ক == (লেন - ১) {
            সিম্বল = "└───";
            নেক্সট-প্যাডিং = "     ";
        }

        যদি ভ্যালিড(ডাইরেক্টরি[ক]) {
            যদি _ফাইল-নাকি-ডাইরেক্টরি(রুট + "\" + ডাইরেক্টরি[ক]) == "ডাইরেক্টরি" {
                দেখাও প্যাডিং + সিম্বল + ডাইরেক্টরি[ক];
               ট্রি(রুট + "\" + ডাইরেক্টরি[ক], প্যাডিং + নেক্সট-প্যাডিং);
            } অথবা {
                দেখাও প্যাডিং + সিম্বল + ডাইরেক্টরি[ক];
            }
        }

        ক = ক + ১;
    } আবার;
} ফেরত;

দেখাও "ট্রি রুট?";
নাম রুট = _রিড-লাইন();
ট্রি(রুট, "");